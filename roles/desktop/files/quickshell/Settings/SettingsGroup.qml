import QtQuick
import "../Common"

// One coherent group of settings. Not a card: the menubar separates its
// modules with a hairline and a gap rather than by giving each one a filled,
// bordered container, and a settings page reads the same way. The group owns
// its section label, the rule that runs from it to the page edge, and the
// optional reset action; callers only supply the rows.
Item {
    id: root

    default property alias content: contentColumn.data
    property string title: ""
    property bool dirty: false
    property int rowSpacing: Theme.panelRowSpacing
    signal resetRequested()

    readonly property int headingHeight: title === "" ? 0 : Theme.sectionHeaderHeight
    readonly property int headingGap: title === "" ? 0 : 8
    readonly property real availableContentHeight: Math.max(0,
        height - headingHeight - headingGap)

    implicitHeight: headingHeight + headingGap + contentColumn.childrenRect.height

    SectionHeader {
        id: heading
        visible: root.title !== ""
        width: parent.width
        label: root.title.toUpperCase()
        dirty: root.dirty
        onResetRequested: root.resetRequested()
    }

    Column {
        id: contentColumn
        y: root.headingHeight + root.headingGap
        width: parent.width
        spacing: root.rowSpacing
    }
}
