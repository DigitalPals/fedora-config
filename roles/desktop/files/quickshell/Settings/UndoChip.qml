import QtQuick
import QtQuick.Controls as Controls
import "../Common"

// Small undo affordance beside a section header or row; the caller shows it
// only while the covered settings differ from their defaults (design v2).
Item {
    id: root

    signal clicked()

    width: Theme.chipHeight
    height: Theme.chipHeight
    activeFocusOnTab: visible
    Accessible.role: Accessible.Button
    Accessible.name: "Reset to default"
    Accessible.onPressAction: root.clicked()
    Controls.ToolTip.visible: mouse.containsMouse
    Controls.ToolTip.text: "Reset to default"

    Keys.onPressed: event => {
        if (event.key === Qt.Key_Return || event.key === Qt.Key_Enter
                || event.key === Qt.Key_Space) {
            root.clicked(); event.accepted = true;
        }
    }

    Rectangle {
        anchors.fill: parent
        radius: Theme.chipRadius
        color: mouse.pressed ? Theme.hoverFillStrong
            : mouse.containsMouse || root.activeFocus ? Theme.hoverFill : "transparent"
        border.width: root.activeFocus ? 1 : 0
        border.color: Theme.accent
    }

    Sym {
        anchors.centerIn: parent
        name: "undo"
        size: Theme.iconSmall
        symWeight: 450
        color: mouse.containsMouse ? Theme.textHi : Theme.textDim
    }

    MouseArea {
        id: mouse
        anchors.fill: parent
        hoverEnabled: true
        cursorShape: Qt.PointingHandCursor
        onClicked: { root.forceActiveFocus(); root.clicked(); }
    }
}
